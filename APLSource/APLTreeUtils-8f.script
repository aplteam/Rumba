:Namespace APLTreeUtils
⍝ Homepage: http://aplwiki.com/APLTreeUtils
⍝
⍝ *** Version 2.9.0 ⋄ 2014-09-14 ***
⍝ 2.9.0 New method "IsDevelopment" included.
⍝ 2.8.3 * ⎕WX and ⎕ML were not localyzed in GoToWebPage
⍝ 2.8.2 * ""Lowercase"" & ""Uppercase"" can now deal with MTVs: matrices of text vectors.
⍝       * ⎕IO was not set in dlb and dtb.
⍝ 2.8.1 New version of ""Lowercase"" & ""Uppercase"" failed on empty numeric vectors in a nested argument.
⍝ 2.8.0 ""Lowercase"" & ""Uppercase"" much faster on most nested vectors now.
⍝ 2.7.0 New method ""DropEmptyZerosAndBlanks"" introduced,
⍝ 2.6.1 ""WriteBytesAs8BitSignedIntegersFromFile"" renamed to ""WriteBytesAs8BitSignedIntegersToFile""
⍝       83 wasn't specified explicitly as data type in ""WriteBytesAs8BitSignedIntegersFromFile""
⍝ 2.6.0 ""ReadFile"" & ""WriteFile"" renamed to ""ReadBytesAs8BitSignedIntegersFromFile""
⍝       and ""WriteBytesAs8BitIntegersFromFile"" to avoid name clashes.
⍝ 2.5.0 New methods ""ReadFile"" and ""WriteFile"" that returns/accept just integers.
⍝ 2.4.4 Bug fix for bug introduced with 2.4.3. Also: test cases added/improved.
⍝ 2.4.3 Performance of Uppercase & Lowercase improved.
⍝ 2.4.2 Bug in Uppercase & Lowercase fixed
⍝ 2.4.1 ReadUtf8File did not convert files corrrectly that use just (⎕UCS 13)
⍝ 2.4.0 ReadUtf8File & WriteUtf8File accept a tie no instead of a filename.
⍝
⍝ Needs at least Dyalog Version 12 and Unicode for "WriteUtf8File" & "ReadUtf8File"
⍝ From version 1.32 on, all fns in this script are tested to be _
⍝ independent from ⎕IO and ⎕ML except when declared to be different (Where..,)


    ∇ array←Uppercase array;ToUpper;⎕ML;⎕IO;length;bool
      ⎕IO←⎕ML←1
      :If ~0∊⍴array
          'ToUpper'⎕NA'I4 USER32.C32|CharUpper',('*A'⊃⍨1+12>{⍎⍵↑⍨¯1+⍵⍳'.'}2⊃'.'⎕WG'APLVersion'),' =0T'
          :If ~0 1∊⍨≡array
              length←⍴¨array
              :Trap 1 10    ⍝ Under extreme conditions this might cause WS FULL or a LIMIT ERROR
                  :If ∨/bool←,(⊂,0)≢¨length
                      (bool/,array)←(bool/,length)⍴¨↓(ToUpper Trans)↑bool/,array
                  :EndIf
              :Else
                  array←(ToUpper Trans)¨array
              :EndTrap
          :Else
              array←(ToUpper Trans)array
          :EndIf
      :EndIf
    ∇

    ∇ array←Lowercase array;ToLower;⎕ML;⎕IO;length;bool
      ⎕IO←⎕ML←1
      :If ~0∊⍴array
          'ToLower'⎕NA'I4 USER32.C32|CharLower',('*A'⊃⍨1+12>{⍎⍵↑⍨¯1+⍵⍳'.'}2⊃'.'⎕WG'APLVersion'),' =0T'
          :If ~0 1∊⍨≡array
              length←⍴¨array
              :Trap 1 10    ⍝ Under extreme conditions this might cause WS FULL or a LIMIT ERROR
                  :If ∨/bool←,(⊂,0)≢¨length
                      (bool/,array)←(bool/,length)⍴¨↓(ToLower Trans)↑bool/,array
                  :EndIf
              :Else
                  array←(ToLower Trans)¨array
              :EndTrap
          :Else
              array←(ToLower Trans)array
          :EndIf
      :EndIf
    ∇

      Trans←{⎕IO←1
      ⍝ This operator can produce a WS FULL that often disappears when →⎕LC is executed.
      ⍝ Therefore it calles itself via a WS FULL guard but just once.
          ⍺←0                   ⍝ The default.
          ⍺:(⍴⍵)⍴2⊃⍺⍺⊂,⍵        ⍝ If ⍺ is true then by now a workspace compaction was performed.
          1::1 ∇ ⍵              ⍝ In case of a WS FULL call itself with a 1 as left argument.
          b←,⍵≠⎕UCS 0
          (0=+/~b):(⍴⍵)⍴2⊃⍺⍺⊂,⍵ ⍝ Does not contain ⎕UCS 0?
          r←b\2⊃⍺⍺⊂b/,⍵         ⍝ Execute it.
          ((~b)/r)←⎕UCS 0       ⍝ Bring ⎕UCS 0 back
          (⍴⍵)⍴r
      }

    IsChar ←{0 2∊⍨10|⎕dr ⍵}  ⍝ Version 12 compatible: is 82=⎕dr in V11

    ∇ r←IsUnicode
    ⍝ Return a 1 if running under Dyalog Unicode
      r←80=⎕DR' '
    ∇

      SplitPath←{
          ⎕ML←⎕IO←1
          ⍺←'/\'
          l←1+-⌊/⍺⍳⍨⌽⍵
          (l↓⍵)(l↑⍵)
      }

      Split←{
          ⎕ML←⎕IO←1
          ⍺←⎕UCS 13 10 ⍝ Default is CR+LF
          (⍴,⍺)↓¨⍺{⍵⊂⍨⍺⍷⍵}⍺,⍵
      }

      Last←{
          ⎕ML←⎕IO←1
          ⍺←'.'
          (⍴,⍵)=where←¯1+⌊/⍺⍳⍨⌽,⍵:0⍴⍵
          0 1∊⍨≡r←(-where)↑⍵:r
          ''⍴r
      }

      Nest←{
          ⎕ML←⎕IO←1
          (⊂∘,⍣(0 1∊⍨≡⍵))⍵
      }

    Enlist←{⎕ml←3 ⋄ ∊⍵}

    ∇ r←IsDevelopment;⎕IO;⎕ML
    ⍝ Returns a one if the function is running under a Dyalog development EXE or DLL.
      ⎕ML←⎕IO←1
      r←'Development'≡4⊃'#'⎕WG'APLVersion'
      r∨←'DLL'≡4⊃'#'⎕WG'APLVersion'   ⍝ May be DLLRT instead!
    ∇

      dmb←{
      ⍝ delete leading, trailing and multiple blanks from scalar or vector.
          ⍺←' '
          ⎕ML←⎕IO←1
          ~0 1∊⍨≡⍵:∇¨⍵
          2=⍴⍴⍵:Mix ∇¨↓⍵
          (,⍺)≡,⍵:''
          w←1↓¯1↓⍺{⍵/⍨~(2⍴⍺)⍷⍵}⍺,⍵,⍺
          (0=⍴⍴⍵)∧1=⍴w:⍬⍴⍵
          w
      }

      dlb←{
      ⍝ Delete leading blanks. Accepts scalar, vector and matrix as argument.
          ⎕IO←1 ⋄ ⎕ML←1
          (2=|≡⍵):∇¨⍵
          (1=⍴⍴⍵):(+/∧\' '=⍵)↓⍵                 ⍝ Vectors (main application)
          (2=⍴⍴⍵):(+/∧\' '=⍵)⌽⍵                 ⍝ Matrix
          (0=⍴⍴⍵):(⎕IO+' '≡⍵)⊃⍵''               ⍝ Scalar
          'Invalid argument'⎕SIGNAL 11
      }

      dtb←{
      ⍝ Delete trailing blanks. Accepts scalar, vector and matrix as argument.
          ⎕IO←1 ⋄ ⎕ML←1
          (2=|≡⍵):∇¨⍵
          (1=⍴⍴⍵):⌽{(+/∧\' '=⍵)↓⍵}⌽⍵            ⍝ Vectors (main application)
          (2=⍴⍴⍵):(-+/∧⌿∧\' '=⌽⍵)↓[2]⍵          ⍝ Matrix
          (0=⍴⍴⍵):(⎕IO+' '≡⍵)⊃⍵''               ⍝ Scalar
          'Invalid argument'⎕SIGNAL 11
      }

    ∇ r←{length}FormatDateTime ts;ts2;formatstring;bool;⎕IO;⎕ML;buffer
⍝ Format the right argument (defaults to ⎕TS if empty) as a string with:
⍝ 'YYYY-MM-DD HH:MM:SS.MILLISECOND
⍝ If the right argument has not 7 but 6 or 3 elements, formatting is done accordingly.
⍝ Via the left argument the length of the right argument can be set to _
⍝ ⍬, 3, 6 or 7; default is 6; ⍬ Accepts any length of the right argument which is 3,6 or 7.
⍝ If the right argument is a simple vector, a string is returned.
⍝ If it is a matrix, a matrix is returned.
      ⎕IO←⎕ML←1
      :If ⍬≡length←{2=⎕NC ⍵:⍎⍵ ⋄ 6}'length'
          length←''⍴¯1↑⍴ts
      :EndIf
      :If ~0∊⍴ts
          :If 2=⍴⍴ts
              buffer←{⍵/⍨0<⊃∘⍴¨⍵~¨⊂' ' 0}↓ts
              'Invalid right argument: must be integer'⎕SIGNAL 11/⍨163≠∪⎕DR¨buffer
              'Invalid right argument: must not be negative'⎕SIGNAL 11/⍨∨/¯1∊¨×¨buffer
              'Invalid right argument: must be simple'⎕SIGNAL 11/⍨1≠∪≡¨buffer
          :Else
              'Invalid right argument: must be integer'⎕SIGNAL 11/⍨163≠⎕DR ts
              'Invalid right argument: must not be negative'⎕SIGNAL 11/⍨¯1∊×,ts
              'Invalid right argument: must be simple'⎕SIGNAL 11/⍨1≠≡ts
          :EndIf
      :EndIf
      :If 2=⍴⍴ts
          ts2←length↑[2]ts
      :Else
          ts2←,[0.5]length↑{0∊⍴⍵:⎕TS ⋄ ts}ts
      :EndIf
      :Select First length
      :Case 3
          formatstring←'ZI4,<->,ZI2,<->,ZI2'
      :CaseList 6 7
          formatstring←'ZI4,<->,ZI2,<->,ZI2,< >,ZI2,<:>,ZI2,<:>,ZI2'
      :Else
          'Invalid left argument'⎕SIGNAL 11
      :EndSelect
      bool←(ts2∨.≠' ')∧ts2∨.≠0
      r←bool⍀formatstring ⎕FMT(6⌊length)↑[2]bool⌿ts2
      :If 7=2⊃⍴ts2
          r←⊃(↓r),¨{0=⍵:'' ⋄ 0∊⍴⍵~' ':'' ⋄ '.',⍕⍵}¨ts2[;7]
      :EndIf
      :If 2≠⍴⍴ts
          r←,r
      :EndIf
    ∇

    ∇ R←CreateUUID;∆UuidCreate;⎕ML;⎕IO
      ⎕ML←⎕IO←1
      '∆UuidCreate'⎕NA'I RPCRT4|UuidCreate >{I1[4] I1[2] I1[2] I1[2] I1[6]}'
      R←2⊃∆UuidCreate 1
      R←'0123456789ABCDEF{-'[1+∊16 17 17 17 17,¨∊¨⍉¨16 16∘⊤¨R],'}'
    ∇

    ∇ r←{flat}ReadUtf8File fileIdentifier;filename;fno;noOfBytes;bytes;⎕IO;⎕ML;b;typeFlag
    ⍝ This function works also with the Classic version if the file to be read does not _
    ⍝ contain "real" Unicode data.
    ⍝ By defaults ""ReadUtf8File"" returns a nested vector were each item carries a record. _
    ⍝ If you want to get a simple stream specify 'flat' as left argument.
    ⍝ ""fileIdentifier"" can be one of:
    ⍝ 1) A filename. Then the file is tied, read and untied by ""ReadUtf8File"".
    ⍝ 2) A file tie number. In this case the file is just read, it reamains tied. Use this _
    ⍝    to tie a file with certain rights, for example 34 which requests read+write but _
    ⍝    grants to others just read.
      ⎕IO←⎕ML←1
      r←''
      flat←{0=⎕NC ⍵:0 ⋄ 'flat'≡⍎⍵}'flat'
      :If typeFlag←' '=1↑0⍴fileIdentifier
          filename←fileIdentifier
          :Trap 19 22
              fno←(filename~'"')⎕NTIE 0
          :Else
              ('Could not read file: ',filename)⎕SIGNAL ⎕EN
          :EndTrap
      :Else
          fno←fileIdentifier
      :EndIf
      noOfBytes←⎕NSIZE fno
      bytes←⎕NREAD fno 83,noOfBytes,0
      :If typeFlag
          ⎕NUNTIE fno
      :EndIf
      bytes+←256×bytes<0                 ⍝ Make sure it is unsigned
      bytes↓⍨←3×239 187 191≡3⍴bytes      ⍝ drop a potential UTF-8 marker
      r←'UTF-8'⎕UCS bytes
      :If ~flat
          :If ∨/(⎕UCS 13 10)⍷r
              r←Split r
          :ElseIf ∨/r=⎕UCS 10
              r←(⎕UCS 10)Split r
          :ElseIf ∨/r=⎕UCS 13
              r←(⎕UCS 13)Split r
          :EndIf
      :EndIf
    ∇

    ∇ r←ReadBytesAs8BitSignedIntegersFromFile filename;No;Size;⎕IO;⎕ML
     ⍝ Read contents of "filename" as 8-bit signed integers.
      :Access Public Shared
      ⎕IO←1 ⋄ ⎕ML←3
      filename~←'"'
      No←filename ⎕NTIE 0,66
      Size←⎕NSIZE No
      r←⎕NREAD No,83,Size,0
      ⎕NUNTIE No
    ∇

    ∇ {r}←WriteBytesAs8BitSignedIntegersToFile(fileIdentifier integers);⎕IO;⎕ML;max;fno;size;flag;i;wasOpenFlag;filename
     ⍝ Write 8-bit integers to "fileIdentifier".
     ⍝ Existing files are replaced.
     ⍝ ""fileIdentifer"" can be one of:
     ⍝ 1) A filename. In this case ""WriteFile"" ties the file, writes _
     ⍝    it and then unties it.
     ⍝ 2) A tie number. In this case the file just written to. Use this to _
     ⍝    tie a file with certain rights like 34: request read & write but _
     ⍝    grant just read.
     ⍝ When the tie fails the function tries a couple of times with an
     ⍝ increasing delay before giving up.
      ⎕IO←⎕ML←1
      r←''
      max←5 ⋄ fno←0⍴i←size←flag←0
      :Repeat
          ⎕DL 1×i>0
          :If wasOpenFlag←0=1↑0⍴fileIdentifier
              fno←fileIdentifier
              size←⎕NSIZE fno
              flag←1
          :Else
              filename←fileIdentifier
              :Trap 19 22
                  fno←(filename~'"')⎕NTIE 0 17  ⍝ Open exclusively
                  size←⎕NSIZE fno
                  flag←1
              :Case 22
                  fno←(filename~'"')⎕NCREATE 0
                  flag←1
              :Else
                  ('Could not open file ',filename)⎕SIGNAL ⎕EN
              :EndTrap
          :EndIf
      :Until flag∨max<i←i+1
      0 ⎕NRESIZE fno
      integers ⎕NAPPEND fno 83
      ⎕NUNTIE(~wasOpenFlag)/fno
    ∇

    ∇ {r}←{append}WriteUtf8File(fileIdentifier data);filename;fno;fullname;flag;⎕ML;⎕IO;i;max;size;simpleFlag;wasOpenFlag
     ⍝ Write UTF-8 "data" to "fileIdentifier" (WITHOUT a BOM!).
     ⍝ If the left argument is string "append" then "data" is appended _
     ⍝ to an already existing file. If there is no such file yet _
     ⍝ it is created no matter what the left argument is.
     ⍝ ""fileIdentifer"" can be one of:
     ⍝ 1) A filename. In this case ""WriteUtf8File"" ties the file, writes _
     ⍝    it and then unties it.
     ⍝ 2) A tie number. In this case the file just written to. Use this to _
     ⍝    tie a file with certain rights like 34: request read & write but _
     ⍝    grant just read.
     ⍝ When the tie fails the function tries a couple of times with an
     ⍝ increasing delay before giving up.
     ⍝ Note that when a nested vector is passed as data then _
     ⍝ CR+LF are added to the end of each item except the very last one. _
     ⍝ When "append" is specified, CR+LF are also added as a prefix to _
     ⍝ the data in case the file does already exist and was not empty yet.
     ⍝ If a simple string is passed it is written as it is: nothing is _
     ⍝ added at all.
      ⎕IO←⎕ML←1
      r←''
      'Invalid data: must not be a matrix'⎕SIGNAL 11/⍨2=⍴⍴data
      append←{2=⎕NC ⍵:⍎⍵ ⋄ ''}'append'
      'Invalid right argument'⎕SIGNAL 11/⍨~(⊂append)∊'append' 1 0 ''⍬
      append←(⊂append)∊'append' 1
      data{⍵:¯2↓⊃,/⍺,¨⊂⎕UCS 13 10 ⋄ ⍺}←~simpleFlag←0 1∊⍨≡data
      max←5 ⋄ fno←0⍴i←size←flag←0
      :Repeat
          ⎕DL 1×i>0
          :If wasOpenFlag←0=1↑0⍴fileIdentifier
              fno←fileIdentifier
              size←⎕NSIZE fno
              flag←1
          :Else
              filename←fileIdentifier
              :Trap 19 22
                  fno←(filename~'"')⎕NTIE 0 17  ⍝ Open exclusively
                  size←⎕NSIZE fno
                  flag←1
              :Case 22
                  fno←(filename~'"')⎕NCREATE 0
                  flag←1
              :Else
                  ('Could not open file ',filename)⎕SIGNAL ⎕EN
              :EndTrap
          :EndIf
      :Until flag∨max<i←i+1
      :If append
          :If (0<size)∧~simpleFlag
              data,⍨←⎕UCS 13 10
          :EndIf
      :EndIf
      :If ~append
      :OrIf append∧fno≡⍬
          0 ⎕NRESIZE fno
      :EndIf
      data←⎕UCS'UTF-8'⎕UCS data             ⍝ Enforce UTF-8
      data ⎕NAPPEND fno
      :If ~wasOpenFlag
          ⎕NUNTIE fno
      :EndIf
    ∇

    ∇ {r}←GoToWebPage url;wsh;⎕IO;⎕WX;⎕ML
    ⍝ Fires up the default browser and goes to "url"
    ⍝ For displaying a file rather then a url add "file://".
    ⍝ However, note that "file://" does not work on some systems.
    ⍝ Examples:
    ⍝ GoToWebPage 'file:///c:/my.html'
    ⍝ GoToWebPage 'file://localhost/c:/my.html'
    ⍝ The two calls are equivalent

      ⎕WX←1 ⋄ ⎕IO←0 ⋄ ⎕ML←3
      r←⍬
      :If ~∨/'file://'⍷Lowercase url
          url←'http://'{((⍺≢Lowercase(⍴⍺)↑⍵)/⍺),⍵}url
      :EndIf
      'wsh'⎕WC'OLEClient' 'WScript.Shell'
      {}wsh.Run url
    ∇

      DropEmptyZerosAndBlanks←{
      ⍝ ⍵ is supposed to be a vector of simple vectors. If it is a _
      ⍝ simple vector instead it will simply survive while a scalar _
      ⍝ will become a one-item vector.
      ⍝ Removes items from ⍵ that are...
      ⍝ * empty
      ⍝ * consist of one or more zeros only
      ⍝ * consist of one or more blanks only
      ⍝ There are faster ways to get rid of empty items but <b>not</b>
      ⍝ of the blank(s) and zero(s) as well!
      ⍝ Examples:
      ⍝ <pre>
      ⍝ 0 ←→ ⍴DropEmptyZerosBlanks '' ⍬ 0 ' ' (0 0) '  '
      ⍝ ('A' 'PL') ←→ DropEmptyZerosBlanks '' ⍬ 0 ' ' (0 0) '  ' 'A' 'PL'
      ⍝ </pre>
          ⎕ML←0
          ~∘∊⍨⍵
      }

      FindPathTo←{
      ⍝ ⍵ is the name of a script (namespace, interface or class).
      ⍝ Tries to find ⍵ in:
      ⍝ 1. The same namespace ⎕THIS is coming from
      ⍝ 2. The namespace where the class (if instanciated) comes from
      ⍝ 3. In #
      ⍝ 4. Where it was called from (NOT the same as 1 and 2)
      ⍝ If it fails to find ⍵, an empty string is returned.
          ⎕IO←1 ⋄ ⎕ML←1
          base←⎕THIS.##                        ⍝ were are we coming from?
          base{11::0 ⋄ 9=⍺.⎕NC ⍵}⍵:base        ⍝ Is it in that namespace?
          base←{11::⍬ ⋄ (1⊃∊⎕CLASS ⍵).##}⍵     ⍝ Where stems the class from?
          base{11::0 ⋄ 9=⍺.⎕NC{Last ⍵}⍕1⊃⊃⎕CLASS ⍵}⍵:base  ⍝ Is it in that namespace?
          9={11::0 ⋄ #.⎕NC ⍵}⍵:#               ⍝ Is it in root?
          path←1⊃⎕RSI~⎕THIS                    ⍝ From where got we called?
          0<path.⎕NC ⍵:path                    ⍝ May be it's there?
          ''                                   ⍝ Give up
      }

    First←{⎕ML←3 ⋄ ↑⍵}                         ⍝ "First" ⎕ml independent
    Mix←{⎕ML←0 ⋄ ↑⍵}                           ⍝ "Mix" ⎕ml independent
    Where←{⎕IO←(⎕IO⊃⎕RSI).⎕IO ⋄ ⍵/⍳⍴,⍵}        ⍝ Return indices for Boolean ⍵; depends on ⎕IO in caller's space

:EndNamespace ⍝ APLTreeUtils
